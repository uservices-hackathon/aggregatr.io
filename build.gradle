buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.6.RELEASE'
        classpath 'com.ofg:uptodate-gradle-plugin:+'
        classpath "io.codearte.accurest:accurest-gradle-plugin:$accurestVersion"
        classpath 'org.ajoberstar:gradle-git:0.12.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'maven-publish'
apply plugin: 'com.ofg.uptodate'
apply plugin: 'accurest'

ext {
    buildNrLoc = project.hasProperty('buildNr') ? "${buildNr}" : "CD-000"
    projectGroupId = groupId

    contractsDir = file("${project.rootDir}/repository/mappings/")
    wiremockStubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
    wiremockStubsOutputDir = new File(wiremockStubsOutputDirRoot, 'mappings/')
}

group = projectGroupId
version = buildNrLoc

sourceCompatibility = '1.8'

configurations {
    all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // To prevent an accidental usage of groovy-all.jar and groovy.jar in different versions
                // all modularized Groovy jars are replaced with groovy-all.jar by default.
                if (details.requested.group == 'org.codehaus.groovy' && details.requested.name != "groovy-all") {
                    details.useTarget("org.codehaus.groovy:groovy-all:${details.requested.version}")
                }
                if (details.requested.name == 'servlet-api') {
                    details.useTarget("org.apache.tomcat.embed:tomcat-embed-core:8.0.23")
                }
                if (details.requested.group == 'org.springframework.boot') { details.useVersion '1.3.0.RC1' }
                if (details.requested.group == 'org.springframework') { details.useVersion '4.2.2.RELEASE' }
            }
        }
    }
}

project.gradle.projectsEvaluated {
    applicationDefaultJvmArgs = project.gradle.startParameter.systemPropertiesArgs.entrySet().collect{"-D${it.key}=${it.value}"}
}

test {
    jvmArgs = project.gradle.startParameter.systemPropertiesArgs.entrySet().collect{"-D${it.key}=${it.value}"} + ['-Xmx512m']
    testLogging {
        exceptionFormat = 'full'
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework:spring-context-support"
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile "org.springframework.cloud:spring-cloud-starter-zipkin:1.0.0.M2"
    compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonMapper"
    compile "org.codehaus.jackson:jackson-core-asl:$jacksonMapper"
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.tomcat.embed:tomcat-embed-el'
    compile 'com.jayway.jsonpath:json-path-assert:2.0.0'
    compile 'org.yaml:snakeyaml'
    compile 'org.hibernate:hibernate-validator'
    compile "org.aspectj:aspectjrt"
    compile "com.ofg:micro-infra-spring-boot-starter:$microInfraSpringVersion"
    compile 'org.projectlombok:lombok:1.16.6'
    compile "de.codecentric:spring-boot-admin-starter-client:1.2.3"
    compile 'org.jolokia:jolokia-core'

    runtime 'cglib:cglib-nodep:3.1'
    runtime 'org.objenesis:objenesis:2.2'
    runtime 'org.aspectj:aspectjweaver'

    testCompile "org.codehaus.groovy:groovy-all"
    testCompile 'com.jayway.awaitility:awaitility:1.6.5'
    compile('com.github.tomakehurst:wiremock:2.0.4-beta') {
        exclude group: 'org.mortbay.jetty', module: 'servlet-api'
    }
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "com.ofg:micro-infra-spring-test:$microInfraSpringVersion"
    testCompile "org.spockframework:spock-spring"
    testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
    testCompile "com.jayway.restassured:spring-mock-mvc:$restAssuredVersion"
}

accurest {
    baseClassForTests = 'pl.uservices.aggregatr.aggregation.BaseMockMvcSpec'
    basePackageForTests = 'pl.uservices'
    contractsDslDir = contractsDir
    generatedTestSourcesDir = file("${project.buildDir}/generated-sources/accurest")
    stubsOutputDir = wiremockStubsOutputDir
}

test.dependsOn { generateAccurest } //See: https://github.com/Codearte/accurest/issues/36

task stubsJar(type: Jar, dependsOn: ["generateWireMockClientStubs"]) {
    baseName = "${project.name}-stubs"
    from wiremockStubsOutputDirRoot
}

bootRepackage {
    withJarTask = "jar"
}

//Make sure that fat jar is built before uploading
project.tasks.findAll { it.name.startsWith('publish') || it.name == 'bintrayUpload' }*.dependsOn { bootRepackage }

artifacts {
    archives stubsJar
}

publishing {
    repositories {
        maven {
            url getProp('mavenRepoUrl')
            credentials {
                username getProp('mavenUser')
                password getProp('mavenPassword')
            }
        }
    }
    publications {
        stubs(MavenPublication) {
            artifactId "${project.name}-stubs"
            artifact stubsJar
        }
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                //Remove dependencies - everything is embedded in JAR created by Spring Boot
                asNode().dependencies.replaceNode{ null }
            }
        }
    }
}

String getProp(String propName) {
    return hasProperty(propName) ? (getProperty(propName) ?: System.properties[propName]) : System.properties[propName]
}

task addHashFile << {
    ext.repo = org.ajoberstar.grgit.Grgit.open(project.file('.'))
    def headCommit = repo.head()
    ext.gitBranch = repo.branch.current.name
    ext.gitCommitId = headCommit.abbreviatedId
    ext.gitCommitTime = new Date(headCommit.time * 1000L).format("yyyy-MM-dd'T'HH:mm:ssZ")

    File resourcesMainDir = new File(project.buildDir, 'resources/main')
    resourcesMainDir.mkdirs()
    File gitPropertiesFile = new File(resourcesMainDir, 'git.properties')
    gitPropertiesFile.text = """git.branch=${gitBranch}
git.commit.id=${gitCommitId}
git.commit.time=${gitCommitTime}
"""
}

jar {
    dependsOn addHashFile
    from "git.properties"
}

bootRepackage {
    withJarTask = "jar"
}

task bootRunDev(type: org.springframework.boot.gradle.run.BootRunTask) {
    group = 'Application'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'APP_ENV', 'dev'
        systemProperty 'CONFIG_FOLDER', 'properties'
        systemProperty 'spring.profiles.active', 'dev,springCloud'
    }
}
